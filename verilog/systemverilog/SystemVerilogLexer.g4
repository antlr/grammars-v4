/*
MIT License

Copyright (c) 2022 Mustafa Said AÄŸca

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

lexer grammar SystemVerilogLexer;
channels { COMMENTS, DIRECTIVES }

ACCEPT_ON : 'accept_on' ;
ALIAS : 'alias' ;
ALWAYS : 'always' ;
ALWAYS_COMB : 'always_comb' ;
ALWAYS_FF : 'always_ff' ;
ALWAYS_LATCH : 'always_latch' ;
AM : '&' ;
AMAM : '&&' ;
AMAMAM : '&&&' ;
AMEQ : '&=' ;
AND : 'and' ;
AP : '\'' ;
AS : '*' ;
ASAS : '**' ;
ASCLCLAS : '*::*' ;
ASEQ : '*=' ;
ASGT : '*>' ;
ASSERT : 'assert' ;
ASSIGN : 'assign' ;
ASSL : '*/' ;
ASSUME : 'assume' ;
AT : '@' ;
ATAT : '@@' ;
AUTOMATIC : 'automatic' ;
BEFORE : 'before' ;
BEGIN : 'begin' ;
BIND : 'bind' ;
BINS : 'bins' ;
BINSOF : 'binsof' ;
BIT : 'bit' ;
BREAK : 'break' ;
BUF : 'buf' ;
BUFIFONE : 'bufif1' ;
BUFIFZERO : 'bufif0' ;
BYTE : 'byte' ;
CA : '^' ;
CAEQ : '^=' ;
CASE : 'case' ;
CASEX : 'casex' ;
CASEZ : 'casez' ;
CATI : '^~' ;
CELL : 'cell' ;
CHANDLE : 'chandle' ;
CHECKER : 'checker' ;
CL : ':' ;
CLASS : 'class' ;
CLCL : '::' ;
CLEQ : ':=' ;
CLOCKING : 'clocking' ;
CLSL : ':/' ;
CMOS : 'cmos' ;
CO : ',' ;
CONFIG : 'config' ;
CONST : 'const' ;
CONSTRAINT : 'constraint' ;
CONTEXT : 'context' ;
CONTINUE : 'continue' ;
COVER : 'cover' ;
COVERGROUP : 'covergroup' ;
COVERPOINT : 'coverpoint' ;
CROSS : 'cross' ;
DEASSIGN : 'deassign' ;
DEFAULT : 'default' ;
DEFPARAM : 'defparam' ;
DESIGN : 'design' ;
DISABLE : 'disable' ;
DIST : 'dist' ;
DL : '$' ;
DLERROR : '$error' ;
DLFATAL : '$fatal' ;
DLFULLSKEW : '$fullskew' ;
DLHOLD : '$hold' ;
DLINFO : '$info' ;
DLNOCHANGE : '$nochange' ;
DLPERIOD : '$period' ;
DLRECOVERY : '$recovery' ;
DLRECREM : '$recrem' ;
DLREMOVAL : '$removal' ;
DLROOT : '$root' ;
DLSETUP : '$setup' ;
DLSETUPHOLD : '$setuphold' ;
DLSKEW : '$skew' ;
DLTIMESKEW : '$timeskew' ;
DLUNIT : '$unit' ;
DLWARNING : '$warning' ;
DLWIDTH : '$width' ;
DO : 'do' ;
DQ : '"' ;
DQDPIDQ : '"DPI"' ;
DQDPIMICDQ : '"DPI-C"' ;
DT : '.' ;
DTAS : '.*' ;
EDGE : 'edge' ;
ELSE : 'else' ;
EM : '!' ;
EMEQ : '!=' ;
EMEQEQ : '!==' ;
EMEQQM : '!=?' ;
END : 'end' ;
ENDCASE : 'endcase' ;
ENDCHECKER : 'endchecker' ;
ENDCLASS : 'endclass' ;
ENDCLOCKING : 'endclocking' ;
ENDCONFIG : 'endconfig' ;
ENDFUNCTION : 'endfunction' ;
ENDGENERATE : 'endgenerate' ;
ENDGROUP : 'endgroup' ;
ENDINTERFACE : 'endinterface' ;
ENDMODULE : 'endmodule' ;
ENDPACKAGE : 'endpackage' ;
ENDPRIMITIVE : 'endprimitive' ;
ENDPROGRAM : 'endprogram' ;
ENDPROPERTY : 'endproperty' ;
ENDSEQUENCE : 'endsequence' ;
ENDSPECIFY : 'endspecify' ;
ENDTABLE : 'endtable' ;
ENDTASK : 'endtask' ;
ENUM : 'enum' ;
EQ : '=' ;
EQEQ : '==' ;
EQEQEQ : '===' ;
EQEQQM : '==?' ;
EQGT : '=>' ;
EVENT : 'event' ;
EVENTUALLY : 'eventually' ;
EXPECT : 'expect' ;
EXPORT : 'export' ;
EXTENDS : 'extends' ;
EXTERN : 'extern' ;
FINAL : 'final' ;
FIRST_MATCH : 'first_match' ;
FOR : 'for' ;
FORCE : 'force' ;
FOREACH : 'foreach' ;
FOREVER : 'forever' ;
FORK : 'fork' ;
FORKJOIN : 'forkjoin' ;
FUNCTION : 'function' ;
GA : '`' -> channel(DIRECTIVES), pushMode(DIRECTIVE_MODE) ;
GENERATE : 'generate' ;
GENVAR : 'genvar' ;
GLOBAL : 'global' ;
GT : '>' ;
GTEQ : '>=' ;
GTGT : '>>' ;
GTGTEQ : '>>=' ;
GTGTGT : '>>>' ;
GTGTGTEQ : '>>>=' ;
HA : '#' ;
HAEQHA : '#=#' ;
HAHA : '##' ;
HAMIHA : '#-#' ;
HIGHZONE : 'highz1' ;
HIGHZZERO : 'highz0' ;
IF : 'if' ;
IFF : 'iff' ;
IFNONE : 'ifnone' ;
IGNORE_BINS : 'ignore_bins' ;
ILLEGAL_BINS : 'illegal_bins' ;
IMPLEMENTS : 'implements' ;
IMPLIES : 'implies' ;
IMPORT : 'import' ;
INCLUDE : 'include' -> mode(LIBRARY_MODE) ;
INITIAL : 'initial' ;
INOUT : 'inout' ;
INPUT : 'input' ;
INSIDE : 'inside' ;
INSTANCE : 'instance' ;
INT : 'int' ;
INTEGER : 'integer' ;
INTERCONNECT : 'interconnect' ;
INTERFACE : 'interface' ;
INTERSECT : 'intersect' ;
JOIN : 'join' ;
JOIN_ANY : 'join_any' ;
JOIN_NONE : 'join_none' ;
LARGE : 'large' ;
LB : '[' ;
LC : '{' ;
LET : 'let' ;
LIBLIST : 'liblist' ;
LIBRARY : 'library' -> mode(LIBRARY_MODE) ;
LOCAL : 'local' ;
LOCALPARAM : 'localparam' ;
LOGIC : 'logic' ;
LONGINT : 'longint' ;
LP : '(' ;
LT : '<' ;
LTEQ : '<=' ;
LTLT : '<<' ;
LTLTEQ : '<<=' ;
LTLTLT : '<<<' ;
LTLTLTEQ : '<<<=' ;
LTMIGT : '<->' ;
MACROMODULE : 'macromodule' ;
MATCHES : 'matches' ;
MEDIUM : 'medium' ;
MI : '-' ;
MICL : '-:' ;
MIEQ : '-=' ;
MIGT : '->' ;
MIGTGT : '->>' ;
MIINCDIR : '-incdir' ;
MIMI : '--' ;
MO : '%' ;
MODPORT : 'modport' ;
MODULE : 'module' ;
MOEQ : '%=' ;
NAND : 'nand' ;
NEGEDGE : 'negedge' ;
NETTYPE : 'nettype' ;
NEW : 'new' ;
NEXTTIME : 'nexttime' ;
NMOS : 'nmos' ;
NOR : 'nor' ;
NOSHOWCANCELLED : 'noshowcancelled' ;
NOT : 'not' ;
NOTIFONE : 'notif1' ;
NOTIFZERO : 'notif0' ;
NULL : 'null' ;
ONESTEP : '1step' ;
OPTION : 'option' ;
OR : 'or' ;
OUTPUT : 'output' ;
PACKAGE : 'package' ;
PACKED : 'packed' ;
PARAMETER : 'parameter' ;
PATHPULSEDL : 'PATHPULSE$' ;
PL : '+' ;
PLCL : '+:' ;
PLEQ : '+=' ;
PLPL : '++' ;
PMOS : 'pmos' ;
POSEDGE : 'posedge' ;
PRIMITIVE : 'primitive' ;
PRIORITY : 'priority' ;
PROGRAM : 'program' ;
PROPERTY : 'property' ;
PROTECTED : 'protected' ;
PULLDOWN : 'pulldown' ;
PULLONE : 'pull1' ;
PULLUP : 'pullup' ;
PULLZERO : 'pull0' ;
PULSESTYLE_ONDETECT : 'pulsestyle_ondetect' ;
PULSESTYLE_ONEVENT : 'pulsestyle_onevent' ;
PURE : 'pure' ;
QM : '?' ;
RAND : 'rand' ;
RANDC : 'randc' ;
RANDCASE : 'randcase' ;
RANDOMIZE : 'randomize' ;
RANDSEQUENCE : 'randsequence' ;
RB : ']' ;
RC : '}' ;
RCMOS : 'rcmos' ;
REAL : 'real' ;
REALTIME : 'realtime' ;
REF : 'ref' ;
REG : 'reg' ;
REJECT_ON : 'reject_on' ;
RELEASE : 'release' ;
REPEAT : 'repeat' ;
RESTRICT : 'restrict' ;
RETURN : 'return' ;
RNMOS : 'rnmos' ;
RP : ')' ;
RPMOS : 'rpmos' ;
RTRAN : 'rtran' ;
RTRANIFONE : 'rtranif1' ;
RTRANIFZERO : 'rtranif0' ;
S_ALWAYS : 's_always' ;
S_EVENTUALLY : 's_eventually' ;
S_NEXTTIME : 's_nexttime' ;
S_UNTIL : 's_until' ;
S_UNTIL_WITH : 's_until_with' ;
SAMPLE : 'sample' ;
SC : ';' ;
SCALARED : 'scalared' ;
SEQUENCE : 'sequence' ;
SHORTINT : 'shortint' ;
SHORTREAL : 'shortreal' ;
SHOWCANCELLED : 'showcancelled' ;
SIGNED : 'signed' ;
SL : '/' ;
SLAS : '/*' ;
SLEQ : '/=' ;
SLSL : '//' ;
SMALL : 'small' ;
SOFT : 'soft' ;
SOLVE : 'solve' ;
SPECIFY : 'specify' ;
SPECPARAM : 'specparam' ;
STATIC : 'static' ;
STD : 'std' ;
STRING : 'string' ;
STRONG : 'strong' ;
STRONGONE : 'strong1' ;
STRONGZERO : 'strong0' ;
STRUCT : 'struct' ;
SUPER : 'super' ;
SUPPLYONE : 'supply1' ;
SUPPLYZERO : 'supply0' ;
SYNC_ACCEPT_ON : 'sync_accept_on' ;
SYNC_REJECT_ON : 'sync_reject_on' ;
TABLE : 'table' -> mode(UDP_MODE) ;
TAGGED : 'tagged' ;
TASK : 'task' ;
THIS : 'this' ;
THROUGHOUT : 'throughout' ;
TI : '~' ;
TIAM : '~&' ;
TICA : '~^' ;
TIME : 'time' ;
TIMEPRECISION : 'timeprecision' ;
TIMEUNIT : 'timeunit' ;
TIVL : '~|' ;
TRAN : 'tran' ;
TRANIFONE : 'tranif1' ;
TRANIFZERO : 'tranif0' ;
TRI : 'tri' ;
TRIAND : 'triand' ;
TRIONE : 'tri1' ;
TRIOR : 'trior' ;
TRIREG : 'trireg' ;
TRIZERO : 'tri0' ;
TYPE : 'type' ;
TYPE_OPTION : 'type_option' ;
TYPEDEF : 'typedef' ;
UNION : 'union' ;
UNIQUE : 'unique' ;
UNIQUEZERO : 'unique0' ;
UNSIGNED : 'unsigned' ;
UNTIL : 'until' ;
UNTIL_WITH : 'until_with' ;
UNTYPED : 'untyped' ;
USE : 'use' ;
UWIRE : 'uwire' ;
VAR : 'var' ;
VECTORED : 'vectored' ;
VIRTUAL : 'virtual' ;
VL : '|' ;
VLEQ : '|=' ;
VLEQGT : '|=>' ;
VLMIGT : '|->' ;
VLVL : '||' ;
VOID : 'void' ;
WAIT : 'wait' ;
WAIT_ORDER : 'wait_order' ;
WAND : 'wand' ;
WEAK : 'weak' ;
WEAKONE : 'weak1' ;
WEAKZERO : 'weak0' ;
WHILE : 'while' ;
WILDCARD : 'wildcard' ;
WIRE : 'wire' ;
WITH : 'with' ;
WITHIN : 'within' ;
WOR : 'wor' ;
XNOR : 'xnor' ;
XOR : 'xor' ;

BINARY_NUMBER : ( SIZE WHITE_SPACE? )? BINARY_BASE WHITE_SPACE? BINARY_VALUE ;
COMMENT : ( ONE_LINE_COMMENT | BLOCK_COMMENT ) -> channel(COMMENTS) ;
DECIMAL_NUMBER : ( ( SIZE WHITE_SPACE? )? DECIMAL_BASE WHITE_SPACE? )? UNSIGNED_NUMBER | ( SIZE WHITE_SPACE? )? DECIMAL_BASE WHITE_SPACE? ( X_DIGIT | Z_DIGIT ) '_'* ;
ESCAPED_IDENTIFIER : '\\' ASCII_PRINTABLE_EXCEPT_SPACE* WHITE_SPACE ;
HEX_NUMBER : ( SIZE WHITE_SPACE? )? HEX_BASE WHITE_SPACE? HEX_VALUE ;
OCTAL_NUMBER : ( SIZE WHITE_SPACE? )? OCTAL_BASE WHITE_SPACE? OCTAL_VALUE ;
REAL_NUMBER : FIXED_POINT_NUMBER | UNSIGNED_NUMBER ( '.' UNSIGNED_NUMBER )? EXP SIGN? UNSIGNED_NUMBER ;
SIMPLE_IDENTIFIER : [a-zA-Z_] [a-zA-Z0-9_$]* ;
STRING_LITERAL : '"' ( ~["\\] | ESC_SEQ )* '"' ;
SYSTEM_TF_IDENTIFIER : '$' [a-zA-Z0-9_$] [a-zA-Z0-9_$]* ;
TIME_LITERAL : ( UNSIGNED_NUMBER | FIXED_POINT_NUMBER ) TIME_UNIT ;
UNBASED_UNSIZED_LITERAL : '\'0' | '\'1' | '\'' [xXzZ] ;
WHITE_SPACE : [ \t\r\n]+ -> channel(HIDDEN) ;
ZERO_OR_ONE_Z_OR_X : [01] [zZxX] ;

mode LIBRARY_MODE;
LIBRARY_COMMENT : COMMENT -> channel(COMMENTS), type(COMMENT) ;
LIBRARY_ESCAPED_IDENTIFIER : ESCAPED_IDENTIFIER -> type(ESCAPED_IDENTIFIER) ;
LIBRARY_MIINCDIR : MIINCDIR -> type(MIINCDIR) ;
LIBRARY_SIMPLE_IDENTIFIER : SIMPLE_IDENTIFIER -> type(SIMPLE_IDENTIFIER) ;
LIBRARY_WHITE_SPACE : WHITE_SPACE -> channel(HIDDEN), type(WHITE_SPACE) ;
LMCO : CO -> type(CO) ;
LMGA : GA -> channel(DIRECTIVES), type(GA), pushMode(DIRECTIVE_MODE) ;
LMSC : SC -> type(SC), mode(DEFAULT_MODE) ;
FILE_PATH_SPEC : ( [a-zA-Z0-9_./] | ESC_SEQ )+ | STRING_LITERAL ;

mode UDP_MODE;
EDGE_SYMBOL : [rRfFpPnN*] ;
LEVEL_ONLY_SYMBOL : [?bB] ;
OUTPUT_OR_LEVEL_SYMBOL : [01xX] ;
UDP_COMMENT : COMMENT -> channel(COMMENTS), type(COMMENT) ;
UDP_ENDTABLE : ENDTABLE -> type(ENDTABLE), mode(DEFAULT_MODE) ;
UDP_WHITE_SPACE : WHITE_SPACE -> channel(HIDDEN), type(WHITE_SPACE) ;
UMCL : CL -> type(CL) ;
UMGA : GA -> channel(DIRECTIVES), type(GA), pushMode(DIRECTIVE_MODE) ;
UMLP : LP -> type(LP) ;
UMMI : MI -> type(MI) ;
UMRP : RP -> type(RP) ;
UMSC : SC -> type(SC) ;

mode DIRECTIVE_MODE;
BEGIN_KEYWORDS_DIRECTIVE : 'begin_keywords' -> channel(DIRECTIVES), mode(DIRECTIVE_TEXT_MODE) ;
CELLDEFINE_DIRECTIVE : 'celldefine' -> channel(DIRECTIVES), popMode ;
DEFAULT_NETTYPE_DIRECTIVE : 'default_nettype' -> channel(DIRECTIVES), mode(DIRECTIVE_TEXT_MODE) ;
DEFINE_DIRECTIVE : 'define' -> channel(DIRECTIVES), mode(DEFINE_DIRECTIVE_MODE) ;
ELSE_DIRECTIVE : 'else' -> channel(DIRECTIVES), popMode, mode(SOURCE_TEXT_MODE) ;
ELSIF_DIRECTIVE : 'elsif' -> channel(DIRECTIVES), popMode, mode(ELSIF_DIRECTIVE_MODE) ;
END_KEYWORDS_DIRECTIVE : 'end_keywords' -> channel(DIRECTIVES), popMode ;
ENDCELLDEFINE_DIRECTIVE : 'endcelldefine' -> channel(DIRECTIVES), popMode ;
ENDIF_DIRECTIVE : 'endif' -> channel(DIRECTIVES), popMode, popMode, popMode ;
FILE_DIRECTIVE : '__FILE__' -> channel(DIRECTIVES), popMode ;
IFDEF_DIRECTIVE : 'ifdef' -> channel(DIRECTIVES), mode(IFDEF_DIRECTIVE_MODE) ;
IFNDEF_DIRECTIVE : 'ifndef' -> channel(DIRECTIVES), mode(IFDEF_DIRECTIVE_MODE) ;
INCLUDE_DIRECTIVE : 'include' -> channel(DIRECTIVES), mode(INCLUDE_DIRECTIVE_MODE) ;
LINE_DIRECTIVE : 'line' -> channel(DIRECTIVES), mode(DIRECTIVE_TEXT_MODE) ;
LINE_DIRECTIVE_ : '__LINE__' -> channel(DIRECTIVES), popMode ;
NOUNCONNECTED_DRIVE_DIRECTIVE : 'nounconnected_drive' -> channel(DIRECTIVES), popMode ;
PRAGMA_DIRECTIVE : 'pragma' -> channel(DIRECTIVES), mode(DIRECTIVE_TEXT_MODE) ;
RESETALL_DIRECTIVE : 'resetall' -> channel(DIRECTIVES), popMode ;
TIMESCALE_DIRECTIVE : 'timescale' -> channel(DIRECTIVES), mode(DIRECTIVE_TEXT_MODE) ;
UNCONNECTED_DRIVE_DIRECTIVE : 'unconnected_drive' -> channel(DIRECTIVES), mode(DIRECTIVE_TEXT_MODE) ;
UNDEF_DIRECTIVE : 'undef' -> channel(DIRECTIVES), mode(UNDEF_DIRECTIVE_MODE) ;
UNDEFINEALL_DIRECTIVE : 'undefineall' -> channel(DIRECTIVES), popMode ;
MACRO_USAGE : DIRECTIVE_ID ( [ \t\r\n]* MACRO_ARGS )? -> channel(DIRECTIVES), popMode ;

mode DIRECTIVE_TEXT_MODE;
DIRECTIVE_TEXT : COMMENT_TEXT NEWLINE -> channel(DIRECTIVES), popMode ;

mode INCLUDE_DIRECTIVE_MODE;
FILENAME : ( '<' ~[<>]+ '>' | STRING_LITERAL ) -> channel(DIRECTIVES), popMode ;
INCLUDE_COMMENT : COMMENT -> channel(COMMENTS), type(DIRECTIVE_COMMENT) ;
INCLUDE_WHITE_SPACE : WHITE_SPACE -> channel(HIDDEN), type(DIRECTIVE_WHITE_SPACE) ;

mode DEFINE_DIRECTIVE_MODE;
DIRECTIVE_COMMENT : COMMENT -> channel(COMMENTS) ;
DIRECTIVE_WHITE_SPACE : WHITE_SPACE -> channel(HIDDEN) ;
MACRO_NAME : DIRECTIVE_ID ( [ \t\r\n]* MACRO_ARGS )? -> channel(DIRECTIVES), mode(MACRO_TEXT_MODE) ;

mode MACRO_TEXT_MODE;
MACRO_ESC_NEWLINE : '\\' NEWLINE -> channel(DIRECTIVES) ;
MACRO_ESC_SEQ : ESC_SEQ -> channel(DIRECTIVES), type(MACRO_TEXT) ;
MACRO_NEWLINE : NEWLINE -> channel(HIDDEN), type(DIRECTIVE_WHITE_SPACE), popMode ;
MACRO_TEXT : ~[\r\n\\]+ -> channel(DIRECTIVES) ;

mode UNDEF_DIRECTIVE_MODE;
DIRECTIVE_IDENTIFIER : DIRECTIVE_ID -> channel(DIRECTIVES), popMode ;
UNDEF_COMMENT : COMMENT -> channel(COMMENTS), type(DIRECTIVE_COMMENT) ;
UNDEF_WHITE_SPACE : WHITE_SPACE -> channel(HIDDEN), type(DIRECTIVE_WHITE_SPACE) ;

mode IFDEF_DIRECTIVE_MODE;
IFDEF_COMMENT : COMMENT -> channel(COMMENTS), type(DIRECTIVE_COMMENT) ;
IFDEF_IDENTIFIER : DIRECTIVE_ID -> channel(DIRECTIVES), type(DIRECTIVE_IDENTIFIER), pushMode(SOURCE_TEXT_MODE) ;
IFDEF_WHITE_SPACE : WHITE_SPACE -> channel(HIDDEN), type(DIRECTIVE_WHITE_SPACE) ;

mode ELSIF_DIRECTIVE_MODE;
ELSIF_COMMENT : COMMENT -> channel(COMMENTS), type(DIRECTIVE_COMMENT) ;
ELSIF_IDENTIFIER : DIRECTIVE_ID -> channel(DIRECTIVES), type(DIRECTIVE_IDENTIFIER), mode(SOURCE_TEXT_MODE) ;
ELSIF_WHITE_SPACE : WHITE_SPACE -> channel(HIDDEN), type(DIRECTIVE_WHITE_SPACE) ;

mode SOURCE_TEXT_MODE;
SOURCE_TEXT : ~[`/]+ -> channel(DIRECTIVES) ;
SOURCE_TEXT_COMMENT : COMMENT -> channel(COMMENTS), type(DIRECTIVE_COMMENT) ;
STGA : GA -> channel(DIRECTIVES), type(GA), pushMode(DIRECTIVE_MODE) ;
STSL : SL -> more ;

fragment ASCII_ANY : [\u0000-\u007f] ;
fragment ASCII_PRINTABLE : [\u0020-\u007e] ;
fragment ASCII_PRINTABLE_EXCEPT_SPACE : [\u0021-\u007e] ;
fragment BINARY_BASE : '\'' [sS]? [bB] ;
fragment BINARY_DIGIT : [01] | X_DIGIT | Z_DIGIT ;
fragment BINARY_VALUE : BINARY_DIGIT ( '_' | BINARY_DIGIT )* ;
fragment BLOCK_COMMENT : '/*' COMMENT_TEXT '*/' ;
fragment COMMENT_TEXT : ASCII_ANY*? ;
fragment DECIMAL_BASE : '\'' [sS]? [dD] ;
fragment DECIMAL_DIGIT : [0-9] ;
fragment DIRECTIVE_ID : ESCAPED_IDENTIFIER | SIMPLE_IDENTIFIER ;
fragment ESC_SEQ : '\\' . ;
fragment EXP : [eE] ;
fragment FIXED_POINT_NUMBER : UNSIGNED_NUMBER '.' UNSIGNED_NUMBER ;
fragment HEX_BASE : '\'' [sS]? [hH] ;
fragment HEX_DIGIT : [0-9a-fA-F] | X_DIGIT | Z_DIGIT ;
fragment HEX_VALUE : HEX_DIGIT ( '_' | HEX_DIGIT )* ;
fragment MACRO_ARGS : '(' ( MACRO_ARGS | ~[()] )* ')' ;
fragment NEWLINE : '\r'? '\n' ;
fragment NON_ZERO_DECIMAL_DIGIT : [1-9] ;
fragment NON_ZERO_UNSIGNED_NUMBER : NON_ZERO_DECIMAL_DIGIT ( '_' | DECIMAL_DIGIT )* ;
fragment OCTAL_BASE : '\'' [sS]? [oO] ;
fragment OCTAL_DIGIT : [0-7] | X_DIGIT | Z_DIGIT ;
fragment OCTAL_VALUE : OCTAL_DIGIT ( '_' | OCTAL_DIGIT )* ;
fragment ONE_LINE_COMMENT : '//' COMMENT_TEXT NEWLINE ;
fragment SIGN : [+\-] ;
fragment SIZE : NON_ZERO_UNSIGNED_NUMBER ;
fragment TIME_UNIT : [munpf]? 's' ;
fragment UNSIGNED_NUMBER : DECIMAL_DIGIT ( '_' | DECIMAL_DIGIT )* ;
fragment X_DIGIT : [xX] ;
fragment Z_DIGIT : [zZ?] ;
