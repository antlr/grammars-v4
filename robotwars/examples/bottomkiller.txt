; ROBOT "BOTTOM KILLER"
; CREATED BY
; BILL EDMUNDS

  -35 TO SPEEDX
  IF X < 100 . 35 TO SPEEDX
   DAMAGE TO D
DOWN
  80 TO SPEEDY
  IF Y < 150 . 150 TO SPEEDY
  IF DAMAGE # D . 2 * SPEEDX TO SPEEDX
  DAMAGE TO D
SLOW
  IF X < 50 . 35 TO SPEEDX
  IF X > 200 . -35 TO SPEEDX
  GOSUB SCAN
  IF Y < 220 GOTO DOWN
  30 TO SPEEDY
  IF Y < 245 GOTO SLOW
  0 TO SPEEDY
SEARCH
  0 TO SPEEDY
  DAMAGE TO D
  GOSUB SIDE
  IF X > 150 GOTO LEFT
  GOTO RIGHT
LEFT
  -120 TO SPEEDX
  335 TO AIM
ABLE
  AIM TO RADAR
  IF RADAR < 0 GOSUB FIRESCAN
  IF X < 30 GOTO SEARCH
  IF SPEEDX = 0 GOTO SEARCH
  GOTO ABLE
RIGHT
  120 TO SPEEDX
  25 TO AIM
BAKER
  AIM TO RADAR
  IF RADAR < 0 GOSUB FIRESCAN
  IF X > 220 GOTO SEARCH
  IF SPEEDX = 0 GOTO SEARCH
  GOTO BAKER
FIRESCAN
  0 - RADAR TO SHOT
  18 * SPEEDX / RADAR + AIM TO AIM
  ENDSUB
SIDE
  90 TO AIM
  AIM TO RADAR
  IF RADAR < 0 GOSUB FIRE
  IF X < 30 . 310 TO AIM
  IF X > 220 . 50 TO AIM
  AIM TO RADAR
  IF RADAR < 0 GOSUB FIRE
  ENDSUB
SPOTTED
  0 TO SPEEDY
  180 + N TO AIM
FIRE
  0 - RADAR TO SHOT
  IF DAMAGE # D ENDSUB
  AIM TO RADAR
  IF RADAR < 0 GOTO FIRE
  ENDSUB
SCAN
  Y + 100 / 10 TO N
  180 + N TO RADAR
  IF RADAR < 0 GOSUB SPOTTED
  0 - N TO N
  180 + N TO RADAR
  IF RADAR < 0 GOSUB SPOTTED
  0 TO N
  180 TO RADAR
  IF RADAR < 0 GOSUB SPOTTED
  ENDSUB
