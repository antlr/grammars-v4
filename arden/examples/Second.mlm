maintenance:
title: Screen for hypercalcemia for Dr. B.'s study;;
mlmname: hypercalcemia_for_b;;
arden: Version 3;;
version: 2.02;;
institution: Columbia-Presbyterian Medical Center;;
author: George Hripcsak, M.D.;;
specialist: ;;
date: 1990-12-04;;
validation: research;;
library:
purpose:
Screen for hypercalcemia for Dr. B.'s study. (This MLM demonstrates
screening patients for clinical trials.);;
explanation:
The storage of a serum calcium value evokes this MLM. If a serum
albumin is available from the same blood sample as the calcium,
then the corrected calcium is calculated, and patients with actual
or corrected calcium greater than or equal 11.5 are accepted; if
such a serum albumin is not available, then patients with actual
calcium greater than or equal 11.0 are accepted. Patients with
serum creatinine greater than 6.0 are excluded from the study.;;
keywords: hypercalcemia;;
citations: ;;
knowledge:
type: data-driven;;
data: /* the storage of a calcium value evokes this MLM */
storage_of_calcium := event {‘06210519’,’06210669’};
/* total calcium in mg/dL */
calcium := read last {‘06210519’,’06210669’;’CALCIUM’};
/* albumin in g/dL */
evoking_albumin := read last {‘06210669’;’ALBUMIN’ where evoking};
/* albumin in g/dL; not necessarily from same test as Ca */
last_albumin := read last ({‘06210669’;’ALBUMIN’}
where it occurred within the past 2 weeks);
/* creatinine in mg/dL; not necessarily from same test as Ca */
creatinine := read last ({‘06210669’,’06210545’,’06000545’;’CREAT’}
where it occurred within the past 2 weeks);
;;
evoke: storage_of_calcium;;
logic: /* make sure the Ca is present (vs. hemolyzed, ...) */
IF calcium is not present THEN
conclude false;
ENDIF;
/* if creatinine is present and greater than 6, then stop now */
IF creatinine is present THEN
IF creatinine is greater than 6.0 THEN
conclude false;
ENDIF;
ENDIF;
/* is an albumin present for the same sample as the calcium */
IF evoking_albumin is present THEN
/* calculate the corrected calcium */
IF evoking_albumin is less than 4.0 THEN
corrected_calcium := calcium + (4.0 - evoking_albumin) * 0.8;
ELSE/* corrected is never less than actual */
corrected_calcium := calcium;
ENDIF;
/* test for total or corrected calcium >= 11.5 */
IF calcium >= 11.5 OR corrected_calcium >= 11.5 THEN
msg := "calcium = " || calcium ||
" on " || time of calcium ||
" (corrected calcium = " ||
corrected_calcium || ")";
msg := msg||"; albumin = "||evoking_albumin;
IF creatinine is present THEN
msg := msg ||
"; last creatinine = "||creatinine;
msg := msg ||
"; (total or corrected calcium " ||
"was at least 11.5)";
conclude true;
ELSE 
conclude false;
ENDIF;
ENDIF;
/* no evoking albumin was present */
ELSE
/* check for true calcium >= 11.0 */
IF calcium >= 11.0 THEN
msg := "calcium = " || calcium || " on " || time of calcium;
IF last_albumin is present THEN
msg := msg || "; last albumin " ||
"(not from same blood sample as calcium) = " ||
last_albumin;
IF creatinine is present THEN
msg := msg || "; last creatinine = "
|| creatinine;
msg := msg ||
"; (total calcium was at least 11.0; " ||
"corrected calcium was not calculated)";
conclude true;
ELSE
conclude false;
ENDIF;
ENDIF;
ENDIF;
ENDIF;
;;
action: write "hypercalcemia study: " || msg;;
urgency: 50;;
end:

maintenance:
title: Check for penicillin allergy;;
mlmname: pen_allergy;;
arden: version 3;;
version: 1.00;;
institution: Columbia-Presbyterian Medical Center;;
author: George Hripcsak, M.D.;;
specialist: ;;
date: 1991-03-18;;
validation: testing;;
library:
purpose:
When a penicillin is prescribed, check for an allergy. (This MLM
demonstrates checking for contraindications.);;
explanation:
This MLM is evoked when a penicillin medication is ordered. An
alert is generated because the patient has an allergy to penicillin
recorded.;;
keywords: penicillin; allergy;;
citations: ;;
knowledge:
type: data-driven;;
data: /* an order for a penicillin evokes this MLM */
penicillin_order := event {medication_order where
class = penicillin};
/* find allergies */
penicillin_allergy := read last {allergy where
agent_class = penicillin};
;;
evoke: penicillin_order;;
logic:
if exist(penicillin_allergy)then
conclude true;
endif;
;;
action: write "Caution, the patient has the following allergy to penicillin documented:"
|| penicillin_allergy;;
urgency: 50;;
end:
