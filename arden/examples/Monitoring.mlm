maintenance:
title: Monitor renal function while taking gentamicin;;
mlmname: gentamicin_monitoring;;
arden: Version 3;;
version: 1.00;;
institution: Columbia-Presbyterian Medical Center;;
author: George Hripcsak, M.D.;;
specialist: ;;
date: 1991-03-19;;
validation: testing;;
library:
purpose:
Monitor the patient's renal function when the patient is taking
gentamicin. (This MLM demonstrates periodic monitoring.);;
explanation:
This MLM runs every five days after the patient is placed on
gentamicin until the medication is stopped. If the serum creatinine
has not been checked recently, then an alert is generated
requesting follow-up. If the serum creatinine has been checked, is
greater than 2.0, and has risen by more than 20 %, then an alert is
generated warning that the patient may be developing renal
insufficiency due to gentamicin.;;
keywords: gentamicin; renal function;;
citations: ;;
knowledge:
type: data-driven;;
data:
/* an order for gentamicin evokes this MLM */
gentamicin_order := event {medication_order where
class = gentamicin};
/* check whether gentamicin has been discontinued */
gentamicin_discontinued :=
read exist{medication_cancellation where class = gentamicin}
where it occurs after eventtime;
/* baseline serum creatinine mg/dl */
baseline_creatinine := read last {serum_creatinine}
where it occurred before eventtime;
/* followup serum creatinine mg/dl */
recent_creatinine := read last {serum_creatinine}
where it occurred within the past 3 days;
;;
evoke: every 5 days for 10 years starting 5 days after time of
gentamicin_order until gentamicin_discontinued;
;;
logic: if recent_creatinine is not present then
no_recent_creatinine := true;
conclude true;
elseno_recent_creatinine := false;
if % increase of (serum_creatinine,
recent_creatinine) > 20 /* % */
and recent_creatinine > 2.0 then
conclude true;
endif;
endif;
;;
action: if no_recent_creatinine then
write "Suggest obtaining a serum creatinine to follow up " ||
"on renal function in the setting of gentamicin.";
else
write "Recent serum creatinine (" || recent_creatinine ||
" mg/dl) has increased, possibly due to renal " ||
"insufficiency related to gentamicin use.";
endif;
;;
urgency: 50;;
resources:
default: en;;
language: en;;
end: