#
# https://github.com/antlr/grammars-v4/blob/master/javascript/Cpp/README.md
#
# Run the command in the .g4 file directory:
# sed -f Cpp/pitch.sed JavaScriptParser.g4 > target/JavaScriptParser.g4
# sed -f Cpp/pitch.sed JavaScriptLexer.g4  > target/JavaScriptLexer.g4
#
# Replace 'this.' TO 'this->'; 
#
s/this\./this->/g

#
# Add the @header{} section after the options {} section.
#
/options {/ {
  h
}

/}/ {
  p
  H
  g
  /options {.*}/ {
    a 
    a // Auto generated by 'pitch.sed' script
    a @header {
    a #include "JavaScriptLexerBase.h"
    a #include "JavaScriptParserBase.h"
    # !compatibility: 'r' Commands As a GNU extension
    r Cpp/custom.h
    a
    a }
  }
}

/}/ {
  z
  h
  d
}

/.*/ {
  H
}